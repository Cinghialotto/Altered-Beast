
MPTR1	EQU	&AC	; Start address of file
MPTR2	EQU	&AE	; End address/Workspace address
TSPEED	EQU	&B0	; Baud rate number
MPTR3	EQU	&BB	; Filename PRT
MFLEN	EQU	&B7	; Length of filename
MCONTRL	EQU	&90	; Control byte
MCHECKSUM	EQU	&9B	; EOR checksum
MBYTE	EQU	&95	; Current byte being formed in int
MLVFLAG	EQU	&93	; Load/Verify flag
MINDEX	EQU	&9C	; Index counter for interrupt driver
INTERLOCK	EQU	&C0	; Cassette motor interlock

;MPTR1	EQU	&EC	; Start address of file
;MPTR2	EQU	&EE	; End address/Workspace address
;TSPEED	EQU	&E0	; Baud rate number
;MPTR3	EQU	&E2	; Filename PRT
;MFLEN	EQU	&E4	; Length of filename
;MCONTRL	EQU	&D0	; Control byte
;MCHECKSUM	EQU	&DB	; EOR checksum
;MBYTE	EQU	&D5	; Current byte being formed in int
;MLVFLAG	EQU	&D3	; Load/Verify flag
;MINDEX	EQU	&DC	; Index counter for interrupt driver
;INTERLOCK	EQU	&F0	; Cassette motor interlock

LOAD	TAY
	LDX	#0
SAVESTUF	LDA	&90,X
	STA	TEMPSAVE,X
	INX
	CPX	#&30
	BCC	SAVESTUF

	LDA	FILESTL-1,Y
	STA	MPTR3
	LDA	FILESTH-1,Y
	STA	MPTR3+1
FILELOOP	LDY	#255
GETNAME	INY
	LDA	(MPTR3),Y
	BEQ	SETNAME1	; end of name
	CMP	#255
	BNE	GETNAME
	LDX	#0
RESTSTUF	LDA	TEMPSAVE,X
	STA	&90,X
	INX
	CPX	#&30
	BCC	RESTSTUF
	RTS		; exit loader
SETNAME1	STY	MFLEN	; file name length
	LDA	#&41
	JSR	FILELOAD
	LDA	#0
	STA	BORDER	; OK
	LDA	#%00100101
	STA	R6510
	LDA	MPTR3
	SEC		; + 1 skip the 0
	ADC	MFLEN
	STA	MPTR3
	BCC	SILLYH
	INC	MPTR3+1
SILLYH	JMP	FILELOOP

FILELOAD	SEI
	STA	MLVFLAG
	STX	XREG+1	;&112E
	STY	YREG+1	;&1130
	LDA	1
	AND	#&DC
	ORA	#1
	STA	1
	LDA	#0
	STA	BRAN+1	;&108C
	LDA	#&7F
	STA	&DC0D
	LDA	#&90
	STA	&DC0D
	LDA	JJUMP+1	;&1069
	STA	&FFFE
	LDA	JJUMP+2	;&106A
	STA	&FFFF
	LDA	#&FE
	STA	&DC04
	LDA	#1
	STA	&DC05
	LDA	#0
	STA	MCONTRL
	CLI
	LDA	MLVFLAG
	AND	#2
	BNE	RET
WAT	LDA	MCONTRL
	CMP	#&FD
	BCC	WAT
GOLOAD	LDA	#&7F
	STA	&DC0D
	LDA	1
	ORA	#&20
	STA	INTERLOCK
	STA	1
	LDA	MCONTRL
	CMP	#&FE
	LDX	MPTR1
	LDY	MPTR1+1	;&AD
RET	RTS
NOTME	STA	&D019
	CLI
	PLA
	RTI
JJUMP	JMP	IRC	;&106b
IRC	PHA
	LDA	&D019
	BMI	NOTME	;&1060
	LDA	&DC0D
	LSR	A
	LDA	#&11
	STA	&DC0E
	BIT	MLVFLAG
	BVC	MISBORD	;&1086
	LDA	&D020
	EOR	#5
	STA	&D020
MISBORD	LDA	&DC0D
	LDA	#0
BRAN	BEQ	NEXTLI	;&108D
NEXTLI	ROL	MBYTE
	LDA	MBYTE
	CMP	#&0F
	BNE	BAKRET	;&10A3
	LDA	#&18
	STA	BRAN+1	;&108C
	LDA	#0
	STA	BRAN2+1	;&10BA
	LDA	#1
	STA	MBYTE
BAKRET	PLA
	RTI
	ROL	MBYTE
	BCC	BAKRET	;&10A3
	LDA	MBYTE
	EOR	MCHECKSUM
	STA	MCHECKSUM
	TXA
	PHA
	TYA
	PHA
	LDA	MBYTE
	LDX	#1
	STX	MBYTE
BRAN2	BCS	NEXLIN2	;&10BB
NEXLIN2	CMP	#&0F
	BEQ	SETIT	;&10C8
RESBAK	LDA	#0
	STA	BRAN+1	;&108C
	LDA	#&FF
	STA	MBYTE
SETIT	LDA	#&1E
	STA	BRAN2+1	;&10BA
RESTREG	PLA
	TAY
	PLA
	TAX
	PLA
	RTI
	JMP	STORE	;&1157
	JMP	ENDCH	;&1181
	CMP	#&0F
	BEQ	RESTREG	;&10CD
	CMP	#&AA
	BNE	RESBAK	;&10BF
	LDA	#&34
	STA	BRAN2+1	;&10BA
	LDA	#0
	STA	MINDEX
	STA	MCHECKSUM
	JMP	RESTREG	;&10CD
	LDY	MINDEX
	CPY	MFLEN
	BCS	YCOMP	;&10F9
	CMP	(MPTR3),Y
	BNE	RESBAK	;&10BF
YCOMP	CPY	#&16
	BCS	WLEFT	;&1109
	CPY	#&10
	BCC	SKSTOR	;&1104
	STA	MINDEX,Y
SKSTOR	INY
	STY	MINDEX
	BNE	RESTREG	;&10CD
WLEFT	LDA	MCHECKSUM
	BNE	INITC	;&1185
	LDA	TSPEED+1	;&B1
	LSR	A
	PHA
	LDA	TSPEED
	ROR	A
	CLC
	ADC	TSPEED
	STA	&DC04
	PLA
	ADC	TSPEED+1	;&B1
	STA	&DC05
	LDA	#&6D
	STA	BRAN2+1	;&10BA
	JMP	RESTREG	;&10CD
	LDA	MLVFLAG
	LSR	A
	BCS	VERF	;&1135
XREG	LDX	#0
YREG	LDY	#0
	STX	MPTR1
	STY	MPTR1+1	;&AD
VERF	LDY	#0
	LDA	MPTR2+1	;&AF
	BNE	DECADR	;&1142
	LDY	MPTR2
	STA	MPTR2
	JMP	NODEC	;&1144
DECADR	DEC	MPTR2+1	;&AF
NODEC	STY	MINDEX
	LDA	#&18
	STA	BRAN2+1	;&10BA
	INC	MCONTRL
	BIT	MLVFLAG
	BVC	GOBAK	;&1154
	INC	&D020
GOBAK	JMP	RESTREG	;&10CD
STORE	LDX	1
	LDY	#4
	STY	1
	LDY	#0
	BIT	MLVFLAG
	BMI	VERIFY	;&1179
	STA	(MPTR1),Y
NOLOER	STX	1
	INC	MPTR1
	BNE	HIGB	;&116D
	INC	MPTR1+1	;&AD
HIGB	DEC	MINDEX
	BNE	GOBAK	;&1154
	LDA	#&1B
	STA	BRAN2+1	;&10BA
	JMP	RESTREG	;&10CD
VERIFY	CMP	(MPTR1),Y
	BEQ	NOLOER	;&1165
	STX	1
	BNE	INITC	;&1185
ENDCH	LDA	MCHECKSUM
	BEQ	REEND	;&1191
INITC	LDA	#&FF
SE1	STA	MCONTRL
	LDA	#&16
	STA	BRAN+1	;&108C
	JMP	RESTREG	;&10CD
REEND	LDA	MPTR2
	ORA	MPTR2+1	;&AF
	BNE	VERF	;&1135
	LDA	#&FD
	BNE	SE1	;&1187

INT1	DT	"INTER1"
	DB	0
	DB	255
INT2	DT	"INTER2"
	DB	0
	DB	255
INT3	DT	"INTER3"
	DB	0
	DB	255
INT4	DT	"INTER4"
	DB	0
	DB	255
INT5	DT	"INTER5"
	DB	0
	DB	255

ENDST1	DT	"END1"
	DB	0
	DB	255
ENDST2	DT	"END2"
	DB	0
	DB	255
ENDST3	DT	"END3"
	DB	0
	DB	255
ENDST4	DT	"END4"
	DB	0
	DB	255
ENDST5	DT	"END5"
	DB	0
	DB	255
ENDST6	DT	"END6"
	DB	0
	DB	255
ENDST7	DT	"END7"
	DB	0
	DB	255
ENDST8	DT	"END8"
	DB	0
	DB	255

FILESTL	DB	>LEV1,>LEV2,>LEV3,>LEV4,>LEV5
	DB	>INT1,>INT2,>INT3,>INT4,>INT5
	DB	>ENDST1,>ENDST2,>ENDST3,>ENDST4
	DB	>ENDST5,>ENDST6,>ENDST7,>ENDST8
FILESTH	DB	<LEV1,<LEV2,<LEV3,<LEV4,<LEV5
	DB	<INT1,<INT2,<INT3,<INT4,<INT5
	DB	<ENDST1,<ENDST2,<ENDST3,<ENDST4
	DB	<ENDST5,<ENDST6,<ENDST7,<ENDST8

LEV1	DT	"CODE1"
	DB	0
	DT	"SPRITES1"
	DB	0
	DT	"ENEMY1"
	DB	0
	DT	"WIZARD"
	DB	0
	DT	"CHARSET1"
	DB	0
	DB	255

LEV2	DT	"CODE2"
	DB	0
	DT	"SPRITES2"
	DB	0
	DT	"ENEMY2"
	DB	0
	DT	"CHARSET2"
	DB	0
	DB	255

LEV3	DT	"CODE3"
	DB	0
	DT	"SPRITES3"
	DB	0
	DT	"ENEMY3"
	DB	0
	DT	"CHARSET3"
	DB	0
	DB	255

LEV4	DT	"CODE4"
	DB	0
	DT	"SPRITES4"
	DB	0
	DT	"ENEMY1"	; same as level 1
	DB	0
	DT	"CHARSET4"
	DB	0
	DB	255

LEV5	DT	"CODE5"
	DB	0
	DT	"SPRITES5"
	DB	0
	DT	"ENEMY5"
	DB	0
	DT	"CHARSET5"
	DB	0
	DB	255

TEMPSAVE	DS	&30


